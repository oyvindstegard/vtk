resourcetype course-group : fellestekst-document {

  properties {
    course-group-name : string,
    status : string,
    course-group-code : string,
    title : derived (course-group-name, course-group-code) eval (course-group-name + " (" + course-group-code + ")") overrides title default course-group-name,
    course-group-scope : string,
    course-group-about : html noextract,
    courses-in-group : html noextract,
    relevant-study-programmes : html noextract,
    course-group-admission : html noextract,
    course-group-other : html noextract,
    studinfo-kontakt : string
  }

  edit-rules {
    status (dropdown),
    course-group-scope (dropdown),
    studinfo-kontakt (class[vrtx-shared-text])
  }

  view {
    ##
    [def title resource-prop-obj-val(".", "title")]
    [def course-group-scope resource-prop-obj-val(".", "course-group-scope")]
    [resource-props "."]
    [def document structured-document()]
    [def course-group-about document.properties.course-group-about]
    [def courses-in-group document.properties.courses-in-group]
    [def relevant-study-programmes document.properties.relevant-study-programmes]
    [def course-group-admission document.properties.course-group-admission]
    [def course-group-other document.properties.course-group-other]
    [def studinfo-kontakt document.properties.studinfo-kontakt]
    <!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
    <html xmlns="http://www.w3.org/1999/xhtml">
      <head>
        <title>[if course-group-scope != null && course-group-scope != ""][val course-group-scope]-[localized 'localized-group'][endif][if title != null && title != ""] - [val title][endif]</title>
      </head>
      <body id="vrtx-course-group">
        <div id="vrtx-content">
          <div id="vrtx-main-content">
            <h1>[if course-group-scope != null && course-group-scope != ""][val course-group-scope]-[localized 'localized-group'][endif][if title != null && title != ""] - [val title][endif]</h1>
            [if course-group-about != null && course-group-about != ""]
              <h2>[localized 'course-group-about']</h2>
              [val course-group-about # unescaped]
            [endif]
            [if courses-in-group != null && courses-in-group != ""]
              <h2>[localized 'courses-in-group']</h2>
              [val courses-in-group # unescaped]
            [endif]
            [if relevant-study-programmes != null && relevant-study-programmes != ""]
              <h2>[localized 'relevant-study-programmes']</h2>
              [val relevant-study-programmes # unescaped]
            [endif]
            [if course-group-admission != null && course-group-admission != ""]
              <h2>[localized 'course-group-admission']</h2>
              [val course-group-admission # unescaped]
            [endif]
            [if course-group-other != null && course-group-other != ""]
              <h2>[localized 'course-group-other']</h2>
              [val course-group-other # unescaped]
            [endif]
          </div>
          <div id="vrtx-additional-content">
            <div id="vrtx-related-content">
              [if studinfo-kontakt != null && studinfo-kontakt != "" && studinfo-kontakt != "ingen-fellestekst"]
                <h2>[localized 'studinfo-kontakt']</h2>
                [call "comp:vis-fellestekst" {"docType":"studinfo-kontakt", "propName":"studinfo-kontakt"}]
              [endif]
            </div>
          </div>
        </div>
      </body>
    </html>
    ##
  }
  
  vocabulary {
    status {
      en : ("current-version" = "Current version", "discontinued-course-group" = "Discontinued course group"),
      no : ("current-version" = "Gjeldende versjon", "discontinued-course-group" = "Nedlagt emnegruppe"),
      nn : ("current-version" = "Gjeldande versjon", "discontinued-course-group" = "Nedlagd emnegruppe")
    },
    course-group-scope {
      en : (range = "1..120"),
      no : (range = "1..120"),
      nn : (range = "1..120")
    }
  }

  localization {
    localized-group : (en : "group", no : "gruppe", nn : "gruppe"),
    header : (en : "Edit course group", no : "Rediger emnegruppe", nn : "Rediger emnegruppe"),
    course-group-name : (en : "Course group name", no : "Emnegruppenavn", nn : "Emnegruppenavn"),
    status : (en : "Status", no : "Status", nn : "Status"),
    course-group-code : (en : "Course group code", no : "Emnegruppekode", nn : "Emnegruppekode"),
    course-group-scope : (en : "Course group scope", no : "Emnegruppeomfang", nn : "Emnegruppeomfang"),
    course-group-about : (en : "About the course group", no : "Om emnegruppen", nn : "Om emnegruppa"),
    relevant-study-programmes : (en : "Relevant study programmes", no : "Relevante studieprogrammer", nn : "Relevante studieprogram"),
    courses-in-group : (en : "Courses in group", no : "Emner i emnegruppen", nn : "Emner i emnegruppa"),
    course-group-admission : (en : "Course group admission", no : "Emnegruppeopptak", nn : "Emnegruppeopptak"),
    course-group-other : (en : "Other", no : "Annet", nn : "Anna"),
    studinfo-kontakt : (en : "Contact", no : "Kontakt", nn : "Kontakt")
  }
}