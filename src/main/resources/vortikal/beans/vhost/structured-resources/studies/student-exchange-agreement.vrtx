resourcetype student-exchange-agreement {

  properties {
    tittel : string,
    title : derived (tittel) eval ("student-exchange-agreement"?localized) overrides title,

    owner-of-agreement : string,
    type-of-agreement : string,
    type-of-agreement-other-text-field : html noextract,

    bachelor : boolean defaultvalue("false"),
    master : boolean defaultvalue("false"),
    phd : boolean defaultvalue("false"),
    profesjonsstudium : boolean defaultvalue("false"),
    specialist : boolean defaultvalue("false"),

    introduction : html noextract,
    who-can-apply : html noextract,
    language : html noextract,
    courses : html noextract,
    length-of-semester : html noextract,

    tuition : boolean,
    scholarship : boolean,
    fees-text-field : html noextract,

    housing : html noextract,
    content : html noextract,

    exchange-leave-text-field : html noextract,
    exchange-leave-shared-text : string,

    contact : string,
    earlier-students : html noextract
  }

  edit-rules {
    tittel (size[1]),
    tittel (class[ui-helper-hidden]),

    type-of-agreement (dropdown),
    type-of-agreement-other-text-field (class[if-type-of-agreement-other]),

    group level (bachelor, master, phd, profesjonsstudium, specialist) (after type-of-agreement-other-text-field),

    who-can-apply tooltip (en : "subjects, grade average, credits, other admission criteria, number of students ",
                           no : "fag, karaktersnitt, studiepoeng, andre opptakskrav, evt. antall plasser",
                           nn : "fag, karaktersnitt, studiepoeng, andre opptakskrav, evt. talet på plassar"),
    language tooltip (en : "Language of instruction, language requirements/recommendations and language courses",
                      no : "Undervisningsspråk, språkkrav/-anbefaling og språkkurs",
                      nn : "Undervisningsspråk, språkkrav/-tilråding og språkkurs"),
    courses tooltip (en : "Courses, options, accreditation",
                     no : "Emner, studiemuligheter, uttelling",
                     nn : "Emner, studiemoglegheiter, uttelling"),

    group fees (tuition, scholarship, fees-text-field) (after length-of-semester),

    fees tooltip (en : "School fees, semester fees, scholarships",
                  no : "skoleavgift, semesteravgift, stipendmuligheter",
                  nn : "skoleavgift, semesteravgift, stipendmoglegheiter"),

    group exchange-leave (exchange-leave-text-field, exchange-leave-shared-text) (after content)
  }

  view {
    ##
    [def name resource-prop-obj-val(".", "studentExchangeUniversityAgreementListing.name")]
    [def country resource-prop-obj-val(".", "studentExchangeUniversityAgreementListing.country")]
    [def city resource-prop-obj-val(".", "studentExchangeUniversityAgreementListing.city")]
    [def picture resource-prop-obj-val(".", "studentExchangeUniversityAgreementListing.picture")]
    [def web-address resource-prop-obj-val(".", "studentExchangeUniversityAgreementListing.webAddress")]
    [def erasmus resource-prop-obj-val(".", "studentExchangeUniversityAgreementListing.erasmusCode")]
    [def locale resource-locale()]
    [def document structured-document()]
    [def owner-of-agreement document.properties.owner-of-agreement]
    [def type-of-agreement document.properties.type-of-agreement]
    [def type-of-agreement-other-text-field document.properties.type-of-agreement-other-text-field]
    [def bachelor document.properties.bachelor]
    [def master document.properties.master]
    [def phd document.properties.phd]
    [def profesjonsstudium document.properties.profesjonsstudium]
    [def specialist document.properties.specialist]
    [def introduction document.properties.introduction]
    [def who-can-apply document.properties.who-can-apply]
    [def language document.properties.language]
    [def courses document.properties.courses]
    [def length-of-semester document.properties.length-of-semester]
    [def tuition document.properties.tuition]
    [def scholarship document.properties.scholarship]
    [def fees-text-field document.properties.fees-text-field]
    [def housing document.properties.housing]
    [def content document.properties.content]
    [def exchange-leave-text-field document.properties.exchange-leave-text-field]
    [def exchange-leave-shared-text document.properties.exchange-leave-shared-text]
    [def contact document.properties.contact]
    [def earlier-students document.properties.earlier-students]
    <!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
    <html xmlns="http://www.w3.org/1999/xhtml">
      <head>
        <title>[call "resource:contextual-title-new"]</title>
      </head>
      <body id="vrtx-student-exchange-agreement">
        <div id="vrtx-content">
          <div id="vrtx-main-content">
            <h1>[localized 'student-exchange-agreement-between'] [val name] [localized 'and'] [val owner-of-agreement]</h1>
            [if who-can-apply != null && who-can-apply != ""]
              <h2>[localized 'who-can-apply']</h2>
              <p>[val who-can-apply # unescaped]</p>
            [endif]
            [if language != null && language != ""]
              <h2>[localized 'language']</h2>
              <p>[val language # unescaped]</p>
            [endif]
            [if courses != null && courses != ""]
              <h2>[localized 'courses']</h2>
              <p>[val courses # unescaped]</p>
            [endif]
            [if length-of-semester != null && length-of-semester != ""]
              <h2>[localized 'length-of-semester']</h2>
              <p>[val length-of-semester # unescaped]</p>
            [endif]
            <h2>[localized 'fees']</h2>
            [if tuition != null]
              <p>[localized 'tuition']: [val tuition]</p>
            [endif]
            [if scholarship != null]
              <p>[localized 'scholarship']: [val scholarship]</p>
            [endif]
            [if fees-text-field != null && fees-text-field != ""]
              <p>[val fees-text-field # unescaped]</p>
            [endif]
            [if housing != null && housing != ""]
              <h2>[localized 'housing']</h2>
              <p>[val housing # unescaped]</p>
            [endif]
            [if content != null && content != ""]
              <h2>[localized 'content']</h2>
              <p>[val content # unescaped]</p>
            [endif]
            [if (exchange-leave-text-field != null && exchange-leave-text-field != "") || (exchange-leave-shared-text != null && exchange-leave-shared-text != "")]
              <h2>[localized 'exchange-leave']</h2>
              [if exchange-leave-text-field != null && exchange-leave-text-field != ""]
                <p>[val exchange-leave-text-field # unescaped]</p>
              [endif]
              [if exchange-leave-shared-text != null && exchange-leave-shared-text != ""]
                <p>[val exchange-leave-shared-text # unescaped]</p>
              [endif]
            [endif]
          </div>
          <div id="vrtx-additional-content">
            <div id="vrtx-student-exchange-agreement-university-info" class="vrtx-frontpage-box vrtx-distach-bottom">
              <table>
                <caption>[localized 'about'] [val name]</caption>
                <tbody>
                  <tr>
                    <td colspan="2"><img src="[val picture]" alt="" /></td>
                  <tr>
                    <th scope="row">[localized 'country']<td>[val country]</td>
                  </tr>
                  <tr>
                    <th scope="row">[localized 'city']<td>[val city]</td>
                  </tr>
                  <tr>
                    <th scope="row">[localized 'webpage']<td><a href="[val web-address]">[val web-address]</a></td>
                  </tr>
                </tbody>
              </table>
            </div>
            <div id="vrtx-student-exchange-agreement-info" class="vrtx-frontpage-box vrtx-distach-bottom">
              <table>
                <caption>[localized 'agreement-info']</caption>
                <tbody>
                  <tr>
                    <th scope="row">[localized 'type-of-agreement']</th>
                    <td>[if type-of-agreement != null && type-of-agreement != "" && type-of-agreement != "other"][val type-of-agreement][elseif type-of-agreement = "other"][val type-of-agreement-other-text-field][endif]</td>
                  </tr>
                  <tr>
                    <th scope="row">[localized 'study-level']</th>
                    <td>
                      [def totCount 0]
                      [def levels #({'prop': {'k': 'bachelor',          'v': bachelor}},
                                    {'prop': {'k': 'master',            'v': master}},
                                    {'prop': {'k': 'phd',               'v': phd}},
                                    {'prop': {'k': 'profesjonsstudium', 'v': profesjonsstudium}},
                                    {'prop': {'k': 'specialist',        'v': specialist}})]
                      [list levels level]
                        [if level.prop.v = "true"]
                          [def totCount totCount + 1]
                        [endif]
                      [endlist]
                      [def count 0]
                      [list levels level][if level.prop.v = "true"][if (count = (totCount - 1)) && totCount > 1] [localized 'and'] [elseif count > 0], [endif][def k level.prop.k][localized k][def count count + 1][endif][endlist]
                    </td>
                  </tr>
                  <tr>
                    <th scope="row">[localized 'owner-of-agreement']</th>
                    <td>[val owner-of-agreement]</td>
                  </tr>
                </tbody>
              </table>
            </div>
            <div id="vrtx-student-exchange-agreement-contact" class="vrtx-frontpage-box vrtx-distach-bottom">
              <h2>[localized 'contact']</h2>
              <p>[val contact]</p>
            </div>
            <div id="vrtx-student-exchange-agreement-contact" class="vrtx-frontpage-box">
              <h2>[localized 'earlier-students']</h2></p>
              [if erasmus != null && erasmus != "" && type-of-agreement != null && type-of-agreement = "erasmus"]<p><a href="http://www.iris.siu.no/iris.nsf/wwwRSV?SearchView&Query=[val erasmus]">[localized 'question']</a></p>[endif]
              [if earlier-students != null && earlier-students != ""]<p>[val earlier-students # unescaped]</p>[endif]
            </div>
          </div>
        </div>
      </body>
    </html>
    ##
  }

  vocabulary {
    type-of-agreement {
      en : ("erasmus" = "Erasmus", "nordplus" = "Nordplus", "nordlys" = "Nordlys", "bilateral" = "Bilateral", "uio-avtale" = "UIO-avtale", "other" = "Other"),
      no : ("erasmus" = "Erasmus", "nordplus" = "Nordplus", "nordlys" = "Nordlys", "bilateral" = "Bilateral", "uio-avtale" = "UIO-avtale", "other" = "Annet"),
      nn : ("erasmus" = "Erasmus", "nordplus" = "Nordplus", "nordlys" = "Nordlys", "bilateral" = "Bilateral", "uio-avtale" = "UIO-avtale", "other" = "Anna")
    },
    tuition {
      en : ("true" = "Yes", "false" = "No"),
      no : ("true" = "Ja", "false" = "Nei"),
      nn : ("true" = "Ja", "false" = "Nei")
    },
    scholarship {
      en : ("true" = "Yes", "false" = "No"),
      no : ("true" = "Ja", "false" = "Nei"),
      nn : ("true" = "Ja", "false" = "Nei")
    }
  }

  localization {
    header : (en : "Edit student exchange agreement", no : "Rediger utvekslingsavtale", nn : "Rediger utvekslingsavtale"),
    student-exchange-agreement : (en : "Student exchange agreement", no : "Utvekslingsavtale", nn : "Utvekslingsavtale"),

    owner-of-agreement : (en : "Owner of agreement", no : "Avtaleeier", nn : "Avtaleeigar"), 
    type-of-agreement : (en : "Type of agreement", no : "Avtaletype", nn : "Avtaletype"),
    type-of-agreement-other-text-field : (en : "Other", no : "Annet", nn : "Annet"),

    level : (en : "Level", no : "Nivå", nn : "Nivå"),
    bachelor : (en : "Bachelor", no : "Bachelor", nn : "Bachelor"),
    master : (en : "Master", no : "Master", nn : "Master"),
    phd : (no : "PhD", nn : "PhD", en : "PhD"),
    profesjonsstudium : (en : " Profesjonsstudium", no : "Profesjonsstudium", nn : "Profesjonsstudium"),
    specialist : (en : "Specialist", no : "Spesialist", nn : "Spesialist"),

    introduction : (en : "Introduction", no : "Innledning", nn : "Innleiing"), 
    who-can-apply : (en : "Who can apply?", no : "Hvem kan søke?", nn : "Kven kan søkje"),
    language : (en : "Language", no : "Språk", nn : "Språk"),
    courses : (en : "What can you study?", no : "Hvilke emner kan jeg ta?", nn : "Kva emne kan eg ta?"),
    length-of-semester : (en : "How long is the semester", no : "Hvor lenge varer semesteret", nn : "Kor lenge varer semesteret"),

    fees : (en : "Fees?", no : "Hva koster det?", nn : "Kva kostar det?"),
    tuition : (en : "Tuition payments", no : "Skolepenger", nn : "Skolepengar"),
    scholarship : (en : "Scholarship", no : "Stipend", nn : "Stipend"),

    housing : (en : "Housing", no : "Bolig", nn : "Bolig"),
    content : (en : "Content", no : "Innhold", nn : "Innhald"),

    exchange-leave : (en : "Exchange?", no : "Vil du dra?", nn : "Vil du dra?"),
    exchange-leave-text-field : (en : "Optional text", no : "Valgfri tekst", nn : "Valfri tekst"),
    exchange-leave-shared-text : (en : "Shared text", no : "Fellestekst", nn : "Fellestekst"),

    about : ( en: "About the", no: "Om", nn: "Om"),
    agreement-info : ( en: "About the agreement", no: "Fakta om avtalen", nn: "Fakta Om avtalen"),
    country : ( en: "Country", no: "Land", nn: "Land"),
    city : ( en: "City", no: "By", nn: "By"),
    webpage : ( en: "Webpage", no: "Nettside", nn: "Nettside"),
    study-level : (en : "Study level", no : "Studienivå", nn : "Studienivå"),
    and : (en : "and", no : "og", nn : "og"),
    contact : (en : "Contact", no : "Kontakt", nn : "Kontakt"),
    earlier-students : (en : "Previous students experiences", no : "Studenter som har vært her tidligere", nn : "Studenter som har vore her tidlegare")
  }

}
