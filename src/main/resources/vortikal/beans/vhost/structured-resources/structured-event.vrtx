resourcetype structured-event : structured-document {

  properties {
    start-date : datetime,
    end-date : datetime,
    location : string,
    mapurl : string,
    organizers : json (organizer:string, organizer-url:string) multiple,
    tags : string multiple overrides tags
  }

  edit-rules {
    caption (size[small]),
    group introImageAndCaption (picture, caption) (after introduction) vertical,
    title (size[large]),
    group timeAndPlace (start-date, end-date, location, mapurl) (after introImageAndCaption) horizontal,
    location (size[small]),
    mapurl (size[small]),
    tags tooltip (en : "(Tag1, Tag2, ...)", no : "(Emneord1, Emneord2, ...)", nn : "(Emneord1, Emneord2, ...)")
  }

  scripts {
    tags autocomplete (minChars : "1")
  }
  
  view-components {
    organizer {
      ##
      [resource-props "."]
      [def document structured-document()]
      [def organizers document.properties.organizers]
      [if organizers != null]
        <div class="vrtx-event-organizers">
        <h2>[localized 'organizers']</h2>
        [list organizers organizer]
          [def organizerName organizer.organizer]
          [def organizerUrl organizer.organizer-url]
          [def count _index + 1]
          [def size _size]
          [def beforeLast size - 1]
          
          [if organizerUrl != null && organizerUrl != ""]
            <a href="[val organizerUrl]"><span class="organizer">[val organizerName]</span></a>[if count != beforeLast && count != size && size > 1],[endif]
          [else]
            <span class="organizer">[val organizerName][if count != beforeLast && count != size && size > 1],[endif]</span>
          [endif]

          [if count = beforeLast && size > 1]
            [localized 'organizer-and']
          [endif]

        [endlist]
        </div>
      [endif]
      ##
    }
  }
  
  view {
    ##
    <!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
    <html xmlns="http://www.w3.org/1999/xhtml">
      <head>
        [call "resource:comments-feed-link"]
        <title>[call "comp:title"]</title>
      </head>
      <body id="vrtx-structured-event">
       <div id="vrtx-content">
         <div id="vrtx-main-content">
            <h1>[call "comp:title"]</h1>
            [call "comp:introduction"]
            <div class="vrtx-byline">
              [call "resource:time-and-place"]
            </div>
            [call "comp:introduction-image"]
            [call "json:field" {"select":"content"}]
            [call "comp:organizer"]
            [call "resource:tags"]
            [call "comp:vrtx-date-info"]
            <div class="vrtx-social-components">
              [call "resource:email-friend"]
              [call "resource:share-at"]
            </div>
            [call "resource:comments"]
          </div>
       </div>
      </body>
    </html>
    ##
  }
  
  localization {
    start-date : (en : "Starts", no : "Starter", nn : "Byrjar"),
    end-date : (en : "Ends", no : "Slutter", nn : "Sluttar"),
    location : (en : "Location", no : "Sted", nn : "Stad"),
    mapurl : (en : "Link to map", no : "Lenke til kart", nn : "Lenke til kart"),
    organizers : (en: "Organizer", no : "Arrangør", nn : "Arrangør"),
    organizer : (en: "Name", no : "Navn", nn : "Namn"),
    organizer-url : (en: "Web address", no : "Webadresse", nn : "Webadresse"),
    organizer-and : (en: "and", no : "og", nn : "og"),
    tags : (en : "Tags", no : "Emneord", nn : "Emneord"),
    timeAndPlace : (en : "Time and place", no : "Tid og sted", nn : "Tid og stad"),
    header : (en : "Edit event", no : "Rediger arrangement", nn : "Rediger arrangement")
  }
}
