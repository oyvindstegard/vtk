resourcetype structured-message {

  properties {
    title : string required overrides title,
    message : html noextract,
    listingDisplayedMessage : derived (message) eval (message?truncate),
    isTruncated : derived (message) eval (message?istruncated),
    link-other-language: resource_ref
  }
  
  view-components {
    vrtx-date-info {
      ##
      [resource-props "."]
      [if publish-date != null]
        <div class="vrtx-date-info">
        <span class="published-date-label">[localized "publishedDate"]</span> <span class="published-date">[val publish-date] </span>
        [if publish-date < lastModified]
          - <span class="last-modified-date">[localized "lastModifiedDate"]</span> <span class="last-modified-date">[val lastModified]</span>
        [endif]
        </div>
      [endif]
      ##
    }
    
    link-other-language {
      ##
      [def link resource-prop(".", "link-other-language")]
      [if link != null]
        <a id="vrtx-change-language-link" href="[val link]">[localized "link-language"]</a>
      [endif]
      ##
    }
  }
  
  view {
    ##
    <!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
    <html xmlns="http://www.w3.org/1999/xhtml">
      <head>
        <title>[call "resource:property" {"id":"title"}]</title>
      </head>
      <body id="vrtx-message">
        <div id="vrtx-content">
          <div id="vrtx-main-content">
            [call "comp:link-other-language"]
            <h1>[call "resource:property" {"id":"title"}]</h1>
            <div>
              [call "json:field" {"select":"message"}]
            </div>
            [call "comp:vrtx-date-info"]
            <div class="vrtx-social-components">
              [call "resource:email-friend"]
              [call "resource:share-at"]
            </div>
            [call "resource:comments"]
          </div>
        </div>
      </body>
    </html>
    ##
  }

  localization {
    title : (en : "Title", no : "Tittel", nn : "Tittel"),
    message : (en : "Message", no : "Beskjed", nn : "Beskjed"),
    header : (en : "Edit message", no : "Rediger beskjed", nn : "Rediger beskjed"),
    publishedDate : (en : "Published", no :"Publisert", nn:"Publisert"),
    lastModifiedDate : (en : "Last modified", no : "Sist endret", nn : "Sist endra"),
    link-language: (en : "Norwegian", no : "English", nn : "English"),
    link-other-language {
      en: (en : "Link to norwegian version", no : "Link to english version", nn : "Link to english version"),
      no: (en : "Lenke til norsk versjon", no : "Lenke til engelsk versjon", nn : "Lenke til engelsk versjon"),
      nn: (en : "Lenke til norsk versjon", no : "Lenke til engelsk versjon", nn : "Lenke til engelsk versjon")
    }
  }
}