resourcetype structured-article : structured-document {

  properties {
    author : string multiple,
    media : media_ref,
    tags : string multiple overrides tags,
    related-content : html noextract,
    hideAdditionalContent : boolean
  }

  edit-rules {
    caption (size[small]),
    group introImageAndCaption (picture, caption) (after introduction) vertical,
    group relatedContent (related-content, hideAdditionalContent) (after tags),
    author (after introImageAndCaption),
    title (size[large]),
    tags tooltip (en : "(tag1, tag2, ...)", no : "(emneord1, emneord2, ...)", nn : "(emneord1, emneord2, ...)"),
    author tooltip (en : "(author1, author2, ...)", no : "(forfatter1, forfatter2, ...)", nn : "(forfatter1, forfatter2, ...)")
  }

  scripts {
    tags autocomplete (minChars : "1")
  }

  view-components {

    author {
      ##
      [def authors resource-prop(".", "author")]
      [if authors != null && authors != ""]
        <span class="vrtx-authors"><span class="vrtx-authors-prefix">[localized "by-line"]</span> 
        [list authors author]
          [if _first]
          [else]
            [if _last] [localized "authorsAnd"] [else], [endif]
          [endif]
        [val author]  
        [endlist]
        </span>
      [endif]
      ##
    }
    
    related-content-in-additional-column {
      ##
      [resource-props "."]
      [def document structured-document()]
      [def thehideAdditionalContent json-attr(document, "properties.hideAdditionalContent")]
      [def theRelated-content json-attr(document, "properties.related-content")]
      
      [if (thehideAdditionalContent = "false")]
        <div id="vrtx-additional-content">
          [if theRelated-content != null]
            <div id="vrtx-related-content">
              [val theRelated-content unescaped]
            </div>
          [endif]
        </div> 
      [endif]
      ##
    }
    
  related-content-in-main-column {
      ##
      [resource-props "."]
      [def document structured-document()]
      [def thehideAdditionalContent json-attr(document, "properties.hideAdditionalContent")]
      [def theRelated-content json-attr(document, "properties.related-content")]
      
      [if (thehideAdditionalContent = "true")]
          [if theRelated-content != null]
            <div id="vrtx-related-content">
              [val theRelated-content unescaped]
            </div>
          [endif]
      [endif]
      ##
    }
    
  }


  view {
    ##
    <!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
    <html xmlns="http://www.w3.org/1999/xhtml">
      <head>
        <resource:comments-feed-link />
        <structured-article:javascript />
        <title><comp:title /></title>
      </head>
      <body id="vrtx-structured-article">
        <div id="vrtx-content"> 
          <div id="vrtx-main-content" class="vrtx-hide-additional-content-${resource:property id=[resource:hideAdditionalContent]}">
            <h1><comp:title /></h1>
            <comp:introduction />
            <comp:introduction-image />
            <resource:media-player />
            <json:field select="content" />
            <comp:related-content-in-main-column />
            <resource:tags />
            <comp:author />
 		    <comp:vrtx-date-info />
            <div class="vrtx-social-components"
              <resource:email-friend />
              <resource:share-at />
          	</div>
          	<resource:comments />
          </div>
          <comp:related-content-in-additional-column  />
        </div>
      </body>
    </html>
    ##
  }
  
  vocabulary {
    hideAdditionalContent {
      en : ("true" = "Yes", "false" = "No"),
      no : ("true" = "Ja", "false" = "Nei"),
      nn : ("true" = "Ja", "false" = "Nei")
    }
  }

  localization {
    by-line : (en : "By", no : "Av", nn : "Av"),
    published : (en : "Published", no : "Publisert", nn : "Publisert"),
    download-media-file : (en : "Download media file", no : "Last ned mediefil", nn : "Last ned mediefil"),
    media: (en : "Mediafile (audio or video)", no : "Mediefil (lyd eller video)", nn : "Mediefil (lyd eller video)"),
    author: (en : "Author", no : "Forfatter", nn : "Forfattar"),
    published-date: (en : "Publishing time", no : "Publiseringstidspunkt", nn : "Publiseringstidspunkt"),
    tags : (en : "Tags", no : "Emneord", nn : "Emneord"),
    hideAdditionalContent : (en : "Hide right-column", no : "Skjul høyre kolonne", nn : "Skjul høyre kolonne"),
    related-content : (en : "Related content", no : "Relatert innhold", nn : "Relatert innhold"),
    relatedContent : (en : "Related content", no : "Relatert innhold", nn : "Relatert innhold"),
    header : (en : "Edit article", no : "Rediger artikkel", nn : "Rediger artikkel"),
    authorsAnd : (en : "and", no : "og", nn : "og")
  }
}
