resourcetype jperson {

  properties {
    username : string required,
    getExternalPersonInfo : boolean,
    firstName : string external:userMetadataLdapService,
    surname : string external:userMetadataLdapService,
    title : derived (firstName, surname) 
           eval (firstName + " " + surname) overrides title,
    position : string external:userMetadataLdapService,
    postalAddress : string multiple external:userMetadataLdapService,
    visitingAddress : string multiple,
    email : string multiple external:userMetadataLdapService,
    phone : string multiple external:userMetadataLdapService,
    picture : image_ref, 
    pressPhoto : image_ref,
    availableHours : string,
    room : string,
    rssFeeds : string multiple,
    tags : string multiple,
    content : html,
    getExternalScientificInformation : boolean,
    scientificInformation: string multiple external:fridaPublicationsService,
    selectedPublications: simple_html
  }

  edit-rules {
	group images (picture, pressPhoto) (after phone) horizontal,
	group hoursRoom (availableHours, room) (after images) horizontal 
  }

  scripts {
    getExternalPersonInfo show-hide onclick (firstName, surname, title, postalAddress, phone, email),
    getExternalScientificInformation show-hide onclick (scientificInformation),
    tags autocomplete (minChars : "1"),
    email multipleinputfields,
    phone multipleinputfields,
    postalAddress multipleinputfields,
    visitingAddress multipleinputfields,
    rssFeeds multipleinputfields
  }
  
  services {
    getExternalPersonInfo userMetadataLdapService requires(username),
    getExternalScientificInformation fridaPublicationsService requires(username)
  }

  view-components {
  
    title {
      ##
      [def v resource-prop "." "title"]
      [val v]
      ##
    }

    contact-information {
      ##
        <table class="contact-info" summary="contact-info">
        <tbody>
            [resource-props "."]
            [def document structured-document]
            [if email]
              [def emails json-attr document "properties.email"]
              [list emails theemail]
               <tr><td>[localized "email"]:&nbsp;</td>
               <td>
                [val theemail]
               </td></tr>
              [endlist]
            [endif]
            [if phone]
             [def phones json-attr document "properties.phone"]
             [list phones thephone]
              <tr><td>[localized "phone"]:&nbsp;</td>
              <td>
               [val thephone]
              </td></tr>
             [endlist]
            [endif]
            [if visitingAddress]
             [def visitingAddresses json-attr document "properties.visitingAddress"]
             [list visitingAddresses visitingAddressesLine]
              <tr><td>[localized "visitingAddress"]:&nbsp;</td>
              <td>
               [val visitingAddressesLine]
              </td></tr>
             [endlist]
            [endif]
            [if room]
              <tr><td>[localized "room"]:&nbsp;</td><td>[val room]</td></tr>
            [endif]
            [if availableHours]
              <tr><td>[localized "availableHours"]:&nbsp;</td><td>[val availableHours]</td></tr>
            [endif]
            [if postalAddress]
              [def postalAddresses json-attr document "properties.postalAddress"]
              [list postalAddresses postalAddressesLine]
                <tr><td>[localized "postalAddress"]:&nbsp;</td>
                <td> 
                  [val postalAddressesLine]
                </td></tr>
              [endlist]
            [endif]
        </tbody>
        </table>
      ##
    }
    
    box-feeds {
      ##
        [resource-props "."]
        [def document structured-document]
        [def feeds json-attr document "properties.rssFeeds"]
        [if feeds]
        [list feeds url]
            <div class="narrow-box-borders">
               <h2>Feed tittel</h2>
               [val url]
            </div>
        [endlist]
        [endif]
      ##
    }
    
    person-image {
      ##
      [def img-uri resource-prop "." "picture"]
      [def img-press-uri resource-prop "." "pressPhoto"]
      [if img-uri]
        [def img resource img-uri]
        [def pixel-width resource-prop img-uri "pixelWidth"]
        
        [if pixel-width]
          [def style concat "width:" pixel-width "px"]
        [endif]
        <div class="vrtx-person-image">
        [if !img]
          <img class="vrtx-person-image" src="[val img-uri]" alt="[localized "introduction-image-alt"]" />
        [else]
          <img class="vrtx-person-image" src="[val img-uri]" alt="[localized "introduction-image-alt"]" />
        [endif]
        [if img-press-uri]
          <a id="vrtx-press-image-link" href="[val img-press-uri]">(lenke til pressebilde)</a> 
        [endif]
        </div>
      [endif]
      ##
    }

    foo {
       ##
       [def query "uri = /json/* AND type IN file"]
       [def result search query]
       [if result]
       <ul>
       [list result res]
         [def title resource-prop res "title"]
         [def uri view-url res]
         <li><a href="[val uri]">[val title]</a></li>
       [endlist]
       </ul>
       [endif]
       ##
    }
    
    scientific-info {
      ##
        [resource-props "."]
        [if scientificInformation]
        <h2>[localized "fridaPublications"]</h2>
          [if selectedPublications][val selectedPublications][endif]
          [if scientificInformation][val scientificInformation][endif]
        [endif]
      ##
    }
    
  }

  view {
    ##    
    <!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
    <html xmlns="http://www.w3.org/1999/xhtml">
      <head>
        <!-- begin css -->
        <person:css />
        <!-- end css -->
        <title><comp:title /></title>
      </head>
      <body>
      <div id="content">
       <div id="left-content">
          <h1><comp:title /></h1>
          <div class="box">
            <div class="box-thirds">
                <comp:person-image />   
            </div>
            <div class="box-two-thirds"><comp:contact-information /></div>
          </div>
          <json:field select="content" />
          <resource:tags />
          <comp:scientific-info />
        </div>
        <div id="right-content">
            <comp:box-feeds /> 
        </div>
      </div>
      <comp:foo />
      </body>
    </html>
    ##
  }
  
  localization {
    username : (en : "Username", no : "Brukernavn", nn : "Brukarnamn"),
    getExternalPersonInfo : (en : "Get external information (ldap)", no : "Hent ekstern informasjon (ldap)", nn : "Hent ekstern informasjon (ldap)"),
    firstName : (en : "First name", no : "Fornavn", nn : "Fornamn"),
    surname : (en : "Surname", no : "Etternavn", nn : "Etternamn"),
    postalAddress : (en : "Address", no : "Adresse", nn : "Adresse"),
    visitingAddress : (en : "Visiting address", no : "Besøksadresse", nn : "Besøksadresse"),
    email : (en : "Email", no : "E-post", nn : "E-post"),
    position : (en : "Position", no : "Stilling", nn : "Stilling"), 
    webPage : (en : "Web page", no : "Webside", nn : "Webside"),
    phone : (en : "Telephone", no : "Telefon", nn : "Telefon"),
    officeNumber : (en : "Office number", no : "Kontor", nn : "Kontor"),
    availableHours : (en : "Available hours", no : "Kontortid", nn : "Kontortid"),
    picture : (en : "Picture", no : "Bilde", nn : "Bilete"),
    getExternalScientificInformation : (en : "Get external scientific information (frida)", no : "Hent ekstern vitenskapelig informasjon (frida)", nn : "Hent ekstern vitenskapelig informasjon (frida)"),
    fridaPublications : (en : "Publications", no : "Publikasjoner", nn : "Publikasjoner"),
    selectedPublications : (en : "Selected publications", no : "Utvalgte publikasjoner", nn : "Utvalgte publikasjoner"),
    tags : (en : "Tags", no : "Emneord", nn : "Emneord"),
    content : (en : "Content", no :"Innhold",nn:"Innhold"),
    rssFeeds : (en : "Links to RSS",no : "Lenker til RSS-strømmer", nn :"Lenker til RSS-strømmer"),
    pressPhoto : (en : "Press photo", no : "Presse bilde", nn : "Presse bilde"),
    room : (en : "Room", no : "Rom", nn : "Rom")
  }
}
