resourcetype exam-info {

  properties {
    tittel : string,
    title : derived (tittel) eval ("exam"?localized + " " + "for"?localized + " " + "courseContext.fs-course-code"?contextual + " - " + "semesterContext.semesterTitle"?contextual) overrides title
  }

  edit-rules {
    tittel (size[1]),
    tittel (class[ui-helper-hidden]),
    title (class[ui-helper-hidden])
  }

  view-components {

    date-format(date) {
      ##
      [def dt to-date(date)]
      [val dt # "full-month-short"]
      ##
    }

    time-format(time, date) {
      ##
      [def time concat(" ", time)]
      [def time concat(date, time)]
      [def dt to-date(time)]
      [localized 'time-prefix'] [val dt # "hours-minutes"]
      ##
    }

    duration-format(duration) {
      ##
      [def hours floor(duration)]
      [def minutes floor(((double(duration) - hours) * 60))]
      [if (hours > 0 || minutes > 0)] ([if hours > 0][val hours] [localized 'time-hours'][endif][if minutes > 0] [val minutes] [localized 'time-minutes'][endif])[endif]
      ##
    }
    
    room(rom) {
      ##
      [if rom.buildingName]
        [if rom.buildingUrl]
          [def url rom.buildingUrl]
        [else]
          [def url concat("https://www.google.no/maps/place/", rom.buildingName)]
        [endif]
        <a href="[val url]">[if rom.roomName][val rom.roomName] [endif][val rom.buildingName]</a>
      [elseif rom.buildingId]
        [if rom.roomId][val rom.roomId] [endif][val rom.buildingId]
      [endif]
      ##
    }

    exam-part (part, isSubPart, addTitle) {
      ##
      [if addTitle = "true"]
        [if isSubPart = "true"]<h3>[else]<h2>[endif]
          [val part.name]
        [if isSubPart = "true"]</h3>[else]</h2>[endif]
      [endif]

      [if part.examDate]
        <p class="exam-date"><strong>[localized 'time']:</strong> [call "comp:date-format" { "date" : part.examDate }][if part.examTime] [call "comp:time-format" { "time" : part.examTime, "date": part.examDate }][endif][if part.durationHours][call "comp:duration-format" { "duration" : part.durationHours }][endif].</p>
      [endif]

      [if part.availableDate]
        <p class="exam-available"><strong>[localized 'task-available']:</strong> [call "comp:date-format" { "date" : part.availableDate }][if part.availableTime] [call "comp:time-format" { "time" : part.availableTime, "date": part.availableDate }][endif]</p>
      [endif]

      [if part.submissionDueDate]
        <p class="exam-submission"><strong>[localized 'task-deadline']:</strong> [call "comp:date-format" { "date" : part.submissionDueDate }][if part.submissionDueTime] [call "comp:time-format" { "time" : part.submissionDueTime, "date": part.submissionDueDate }][endif]</p>
      [endif]
      
      [if part.attendanceBefore]
        <p class="exam-attendance-before"><strong>[localized 'task-attendance-before']:</strong> [localized 'attendance-before-text-prefix'] [val part.attendanceBefore] [localized 'attendance-before-text-postfix'].</p>
      [endif]

      [def rooms part.rooms]
      [if rooms != null && rooms.length() > 0]
        [if rooms.length() > 1]
          <p class="exam-place-info"><strong>[localized 'place']:</strong></p>
          <ul class="exam-rooms">
          [list rooms r]
            <li>
              [call "comp:room" { "rom": r }]
            </li>
          [endlist]
          </ul>
          <p class="exam-place-info-multiple-rooms">[localized 'place-info'] <a href="//studentweb.uio.no/">Studentweb</a>.</p>
        [else]
          <p class="exam-place-info"><strong>[localized 'place']:</strong> [list rooms r][call "comp:room" { "rom": r }][endlist]</p>
        [endif]
      [endif]

      [if part.datasystem]
        <p class="exam-datasystem"><strong>[localized 'system']:</strong> </p>
        [call "include:shared-text" { "folder": "/vrtx/fellestekst/eksamen", "file": "system", "key": part.datasystem }]
      [endif]

      [if part.comment]
        <p class="exam-comment"><strong>[localized 'comment']:</strong> [val part.comment]</p>
      [endif]

      [if part.announcement]
        <p class="exam-announcement"><strong>[localized 'announcement']:</strong> [call "comp:date-format" { "date" : part.announcement }]</p>
      [endif]
      ##
    }

    exam-ordning (ordning, termNr, isMoreThanOneArrangements, count) {
      ##

      [if ordning.deadlineWithdrawal != null && isMoreThanOneArrangements = false]
        <h2>[localized 'trekkfrist']</h2>
        <p>[call "comp:date-format" { "date" : ordning.deadlineWithdrawal }]</p>
      [endif]
      [if count = 0]
        <h2>[localized 'ordning']</h2>
      [endif]

      [if isMoreThanOneArrangements]
        <h2 class="accordion">[val ordning.name]</h2>
        [if ordning.deadlineWithdrawal]
          <p class="exam-deadline-withdrawal"><strong>[localized 'trekkfrist']:</strong> [call "comp:date-format" { "date" : ordning.deadlineWithdrawal }]</p>
        [endif]
      [else]
        <p>[val ordning.name].</p>
      [endif]

      [call "comp:exam-part" { "part" : ordning, "isSubPart": "false", "addTitle": "false" }]

      [def examParts ordning.examParts]
      [if examParts != null && examParts.length() > 0]
        [list examParts examPart]
          [if (examPart.termNr = null || termNr = null || (int(examPart.termNr) = int(termNr)))]
            [if isMoreThanOneArrangements]
              [call "comp:exam-part" { "part" : examPart, "isSubPart": "true", "addTitle": "true" }]
            [else]
              [call "comp:exam-part" { "part" : examPart, "isSubPart": "false", "addTitle": "true" }]
            [endif]

            [def partDays examPart.partDays]
            [if partDays]
              [list partDays partDay]
                [call "comp:exam-part" { "part" : partDay, "isSubPart": "true", "addTitle": "true" }]
              [endlist]
            [endif]
          [endif]
        [endlist]
      [else]

        [def partDays ordning.partDays]
        [if partDays]
          [list partDays partDay]
            [call "comp:exam-part" { "part" : partDay, "isSubPart": "true", "addTitle": "true" }]
          [endlist]
        [endif]
      [endif]
      ##
    }
  }

  view {
    ##
    [def settings settings()]
    [def document structured-document()]
    [def locale resource-locale()]
    <!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
    <html xmlns="http://www.w3.org/1999/xhtml">
      <head>
        <title>[call "resource:contextual-title-new"]</title>
      </head>
      <body id="vrtx-exam-info">
        <div id="vrtx-content">
          <div id="vrtx-main-content" class="vrtx-hide-additional-content-true">
            <h1>[localized 'exam']</h1>

            [if ! settings.services.examInfo]
              <p>[localized 'external-service-unavailable'].</p>
            [else]
              [def course-code resource-prop-obj-val(".", "courseContext.fs-course-code")]
              [def term resource-prop-obj-val(".", "semesterContext.semesterTerm")]
              [def termNr resource-prop-obj-val(".", "semesterContext.semesterTermNumber")]
              [def year resource-prop-obj-val(".", "semesterContext.semesterYear")]
              [if year != null && year.length() = 4]
                [def year year.substring(2, 4)]
              [endif]
              [if locale != null]
                [if locale = "no" || locale = "no_NO"]
                  [def lang "no"]
                [elseif locale = "nn" || locale = "no_NO_NY"]
                  [def lang "nn"]
                [else]
                  [def lang "en"]
                [endif]
              [else]
                [def lang "no"]
              [endif]

              [def examInfoURL settings.services.examInfo]

              [if course-code != null && term != null && year != null]
                [def examInfoURL replace(examInfoURL, "%cid", course-code)]
                [def examInfoURL replace(examInfoURL, "%sem", year)]
                [def examInfoURL replace(examInfoURL, "%term", term)]
                [def examInfoURL replace(examInfoURL, "%lang", lang)]
                [def examInfoURL replace(examInfoURL, "Æ", "%C3%86")]
                [def examInfoURL replace(examInfoURL, "Ø", "%C3%98")]
                [def examInfoURL replace(examInfoURL, "Å", "%C3%85")]
                [def resp url-as-json(examInfoURL)]
              [else]
                [def resp { "errorMessage": "Missing context values for course or semester" }]
              [endif]

              [if resp.errorMessage]
                <p>[localized 'lookup-unable']: [val examInfoURL]</p>
                <p>[localized 'lookup-unable-error-msg']: [val resp.errorMessage]</p>
              [else]
                [def arrangements resp.payload.arrangements]
                [if arrangements != null && arrangements.length() > 0]

                  [def arrCount 0]
                  [list arrangements arr]
                    [if arr.status = "Ordinær eksamen" || arr.status = "Ordinary examinaton"]
                      [def arrCount arrCount + 1]
                    [endif]
                  [endlist]
                  [def isMoreThanOneArrangements (arrCount > 1)]

                  [def count 0]
                  [list arrangements arr]
                    [if arr.status = "Ordinær eksamen" || arr.status = "Ordinary examinaton"]
                      [call "comp:exam-ordning" { "ordning" : arr, "termNr": termNr, "isMoreThanOneArrangements": isMoreThanOneArrangements, "count": count }]
                      [def count count + 1]
                    [endif]
                  [endlist]

                  <h2>[localized 'results']</h2>
                  <p>[localized 'results-info'] <a href="//studentweb.uio.no/">Studentweb</a>.</p>

                  <h2>[localized 'more-about']</h2>
                  <p>[localized 'more-about-info'] <a href="./../../#exam">[localized 'more-about-info-course-pages-link']</a>.</p>
                [else]
                  <p>[localized 'no-data'].</p>

                  <h2>[localized 'more-about']</h2>
                  <p>[localized 'more-about-info'] <a href="./../../#exam">[localized 'more-about-info-course-pages-link']</a>.</p>
                [endif]
              [endif]
            [endif]
          </div>
        </div>
      </body>
    </html>
    ##
  }

  localization {
    exam : (en : "Examination: Time and place", no : "Eksamen: Tid og sted", nn : "Eksamen: Tid og stad"),
    for : (en : "for", no : "for", nn : "for"),
    title : (en : "Title", no : "Tittel", nn : "Tittel"),
    header : (en : "Edit exam", no : "Rediger eksamen", nn : "Rediger eksamen"),
    trekkfrist : (en : "Deadline for withdrawing from the exam", no : "Trekkfrist", nn : "Trekkfrist"),
    ordning : (en : "Examination", no : "Eksamensordning", nn : "Eksamensordning"),
    time : (en : "Time", no : "Tid", nn : "Tid"),
    time-hours : (en : "hours", no : "timer", nn : "timer"),
    time-minutes : (en : "minutes", no : "minutter", nn : "minutter"),
    time-prefix : (en : "at", no : "kl.", nn : "kl."),
    place : (en : "Place", no : "Sted", nn : "Stad"),
    place-info : (en : "Information about where to meet can be found in",
                  no : "Du finner informasjon om hvor du skal møte ved å logge på",
                  nn : "Du finn informasjon om kvar du skal møte ved å logge på"),
    system : (en : "Examination system", no : "Eksamenssystem", nn : "Eksamenssystem"),
    comment : (en : "Comment", no : "Merknad", nn : "Merknad"),
    announcement : (en : "Announcement of examination results", no : "Kunngjøring av eksamensresultater", nn : "Kunngjering av eksamensresultat"),
    task-available : (en : "Disclosure of exam assignment", no : "Utlevering av oppgaven", nn : "Utlevering av oppgåva"),
    task-deadline : (en : "Submission deadline", no : "Innleveringsfrist", nn : "Innleveringsfrist"),
    task-attendance-before : (en : "Attendance", no : "Oppmøte", nn : "Oppmøte"),
    results : (en : "Examination results", no : "Eksamensresultater", nn : "Eksamensresultatar"),
    attendance-before-text-prefix : (en : "Meet no later than", no : "Senest", nn : "Seinast"),
    attendance-before-text-postfix : (en : "minutes before the examination starts", no : "minutter før eksamen begynner", nn : "minutter før eksamen begynner"),
    results-info : (en : "You will find your examination results by logging on to",
                    no : "Du finner eksamensresultatene dine i",
                    nn : "Du finn eksamensresultatane dine i"),
    more-about : (en : "More about the examination",
                  no : "Mer om eksamen for dette emnet",
                  nn : "Meir om eksamen i dette emnet"),
    more-about-info : (en : "You will find more information about examination related to this course in the",
                       no : "Mer informasjon om eksamen for dette emnet finner du på",
                       nn : "Meir informasjon om eksamen i dette emnet finn du på"),
    more-about-info-course-pages-link : (en : "course description", no : "emnesiden", nn : "emnesida"),
    no-data : (en : "Information about time and place for the exam is not published yet for this course",
               no : "Informasjon om tid og sted for eksamen er ikke publisert for dette emnet",
               nn : "Informasjon om tid og stad for eksamen er ikkje publisert for dette emne"),
    lookup-unable : (en : "Could not look up exam info", no : "Kunne ikke hente eksamensinfo", nn : "Kunne ikkje henta eksamensinfo"),
    lookup-unable-error-msg : (en : "Error message", no : "Feilbeskjed", nn : "Feilbeskjed"),
    external-service-unavailable : (en: "No external service available", no : "Ingen ekstern tjeneste tilgjengelig", nn : "Ingen ekstern teneste tilgjengeleg")
  }
}
