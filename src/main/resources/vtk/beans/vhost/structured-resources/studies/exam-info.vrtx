resourcetype exam-info {

  properties {
    tittel : string,
    title : derived (tittel) eval ("exam"?localized + " " + "for"?localized + " " + "courseContext.fs-course-code"?contextual + " - " + "semesterContext.semesterTitle"?contextual) overrides title
  }

  edit-rules {
    tittel (size[1]),
    tittel (class[ui-helper-hidden]),
    title (class[ui-helper-hidden])
  }

  view-components {
  
    date-format(date) {
      ##
      [def dt to-date(date)]
      [val dt # "full-month-short"]
      ##
    }
    
    duration-format(duration) {
      ##
      [def hours floor(duration)]
      [def minutes floor(((double(duration) - hours) * 60))]
      [if (hours > 0 || minutes > 0)] ([if hours > 0][val hours] [localized 'time-hours'][endif][if minutes > 0] [val minutes] [localized 'time-minutes'][endif])[endif]
      ##
    }

    exam-part (part, isSubPart) {
      ##
      [if isSubPart = "true"]<h3>[else]<h2>[endif]
        [val part.name]
      [if isSubPart = "true"]</h3>[else]</h2>[endif]
      
      [if part.dateExam]
        <p class="exam-date"><strong>[localized 'time']:</strong> [call "comp:date-format" { "date" : part.dateExam }][if part.timeExam] [localized 'time-prefix'] [val part.timeExam][endif][if part.durationHours][call "comp:duration-format" { "duration" : part.durationHours }][endif].</p>
      [endif]
      
      [if part.submissionDeadline]
        <p class="exam-submission"><strong>[localized 'task-deadline']:</strong> [call "comp:date-format" { "date" : part.submissionDeadline }][if part.timeSubmission] [localized 'time-prefix'] [val part.timeSubmission][endif]</p>
      [endif]
      
      [def room part.room]
      [if part.dateExam != null || room != null]
        <p class="exam-place-info"><strong>[localized 'place']:</strong> [localized 'place-info'] <a href="//studentweb.uio.no/">StudentWeb</a>.</p>
      [endif]
      [if room]
        <ul class="exam-rooms">
        [list room r]
          <li>
            [if r.buildingName]
              [if r.buildingUrl]
                [def url r.buildingUrl]
              [else]
                [def url concat("https://www.google.no/maps/place/", r.buildingName)]
              [endif]
              <a href="[val url]">[if r.roomName][val r.roomName] [endif][val r.buildingName]</a>
            [elseif r.buildingCode]
              [if r.url]
                <a href="[val r.url]">
              [endif]
              [if r.roomCode][val r.roomCode] [endif][val r.buildingCode]
              [if r.url]
                </a>
              [endif]
            [endif]
          </li>
        [endlist]
        </ul>
      [endif]
      
      [if part.datasystem]
        <p class="exam-datasystem"><strong>[localized 'system']:</strong></p>
        [call "include:shared-text" { "folder": "/vrtx/fellestekst/eksamen", "file": "system", "key": part.datasystem }]
      [endif]
      
      [if part.comment]
        <p class="exam-comment"><strong>[localized 'comment']:</strong> [val part.comment]</p>
      [endif]
      
      [if part.announcement]
        <p class="exam-announcement"><strong>[localized 'announcement']:</strong> [val part.announcement]</p>
      [endif]
      ##
    }

    exam-ordning (ordning) {
      ##
      [if ordning.deadlineWithdrawal]
        <h2>[localized 'trekkfrist']</h2>
        <p>[call "comp:date-format" { "date" : ordning.deadlineWithdrawal }]</p>
      [endif]

      [def examParts ordning.examPart]
      [if examParts]
        [def isOrdningEksamensDel (ordning.dateExam != null || ordning.submissionDeadline != null)]

        [def len examParts.length()]
        [if len > 1 && ! isOrdningEksamensDel]
          <h2>[localized 'ordning']</h2>
          <p>[val ordning.name].</p>
        [endif]
      
        [if isOrdningEksamensDel]
          [call "comp:exam-part" { "part" : ordning, "isSubPart": "false" }]
        [endif]

        [list examParts examPart]
          [call "comp:exam-part" { "part" : examPart, "isSubPart": "false" }]
          
          [def partDays examPart.partDay]
          [if partDays]
            [list partDays partDay]
              [call "comp:exam-part" { "part" : partDay, "isSubPart": "true" }]
            [endlist]
          [endif]
        [endlist]
      [else]
        [call "comp:exam-part" { "part" : ordning, "isSubPart": "false" }]

        [def partDays ordning.partDay]
        [if partDays]
          [list partDays partDay]
            [call "comp:exam-part" { "part" : partDay, "isSubPart": "true" }]
          [endlist]
        [endif]
      [endif]
      
      <h2>[localized 'results']</h2>
      <p>[localized 'results-info'] <a href="//studentweb.uio.no/">StudentWeb</a>.</p>
      
      <h2>[localized 'more-about']</h2>
      <p>[localized 'more-about-info'] <a href="./../../#exam">[localized 'more-about-info-course-pages-link']</a>.</p>
      ##
    }
  }

  view {
    ##
    [def settings settings()]
    [def document structured-document()]
    <!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
    <html xmlns="http://www.w3.org/1999/xhtml">
      <head>
        <title>[call "resource:contextual-title-new"]</title>
      </head>
      <body id="vrtx-exam-info">
        <div id="vrtx-content">
          <div id="vrtx-main-content" class="vrtx-hide-additional-content-true">
            <h1>[call "resource:property" {"id":"title"}]</h1>

            [if ! settings.services.examInfo]
              <p>[localized 'external-service-unavailable'].</p>
            [else]
              [def course-code resource-prop-obj-val(".", "courseContext.fs-course-code")]
              [def term resource-prop-obj-val(".", "semesterContext.semesterTerm")]
              [def year resource-prop-obj-val(".", "semesterContext.semesterYear")]
              [if year != null && year.length() = 4]
                [def year year.substring(2, 4)]
              [endif]
              
              [def examInfoURL settings.services.examInfo]
              
              [if course-code != null && term != null && year != null]
                [def examInfoURL replace(examInfoURL, "%cid", course-code)]
                [def examInfoURL replace(examInfoURL, "%sem", year)]
                [def examInfoURL replace(examInfoURL, "%term", term)]
              
                [def resp url-as-json(examInfoURL)]
              [else]
                [def resp { "errorMessage": "Missing context values for course or semester" }]
              [endif]

              [if resp.errorMessage]
                <p>[localized 'lookup-unable']: [val examInfoURL]</p>
                <p>[localized 'lookup-unable-error-msg']: [val resp.errorMessage]</p>
              [else]
                [def exam resp.payload.exams]
                [def arrangement exam.arrangement]
                [if arrangement]
                  [list arrangement arr]
                    [if arr.status = "Ordinær eksamen"]
                      [call "comp:exam-ordning" { "ordning" : arr }]
                    [endif]
                  [endlist]
                [endif]
              [endif]
            [endif]
          </div>
        </div>
      </body>
    </html>
    ##
  }

  localization {
    exam : (en : "Examination: Time and place", no : "Eksamen: Tid og sted", nn : "Eksamen: Tid og stad"),
    for : (en : "for", no : "for", nn : "for"),
    title : (en : "Title", no : "Tittel", nn : "Tittel"),
    header : (en : "Edit exam", no : "Rediger eksamen", nn : "Rediger eksamen"),
    trekkfrist : (en : "Deadline for withdrawing from the exam", no : "Trekkfrist", nn : "Trekkfrist"),
    ordning : (en : "Examination", no : "Eksamensordning", nn : "Eksamensordning"),
    time : (en : "Time", no : "Tid", nn : "Tid"),
    time-hours : (en : "hours", no : "timer", nn : "timer"),
    time-minutes : (en : "minutes", no : "minutter", nn : "minutter"),
    time-prefix : (en : "at", no : "kl.", nn : "kl."),
    place : (en : "Place", no : "Sted", nn : "Stad"),
    place-info : (en : "Information about where to meet can be found in",
                  no : "Du finner informasjon om hvor du skal møte ved å logge på",
                  nn : "Du finn informasjon om kvar du skal møte ved å logge på"),
    system : (en : "Examination system", no : "Eksamenssystem", nn : "Eksamenssystem"),
    comment : (en : "Comment", no : "Merknad", nn : "Merknad"),
    announcement : (en : "Announcement of examination results", no : "Kunngjøring av eksamensresultater", nn : "Kunngjering av eksamensresultat"),
    task-disclosure : (en : "Disclosure of exam assignment", no : "Utlevering av oppgaven", nn : "Utlevering av oppgåva"),
    task-deadline : (en : "Submission deadline", no : "Innleveringsfrist", nn : "Innleveringsfrist"),
    results : (en : "Examination results", no : "Eksamensresultater", nn : "Eksamensresultatar"),
    results-info : (en : "You will find your examination results by logging on to",
                    no : "Du finner eksamensresultatene dine i",
                    nn : "Du finn eksamensresultatane dine i"),
    more-about : (en : "More about the examination",
                  no : "Mer om eksamen for dette emnet",
                  nn : "Meir om eksamen i dette emnet"),
    more-about-info : (en : "You will find more information about examination related to this course in the",
                       no : "Mer informasjon om eksamen for dette emnet finner du på",
                       nn : "Meir informasjon om eksamen i dette emnet finn du på"),
    more-about-info-course-pages-link : (en : "course description", no : "emnesiden", nn : "emnesida"),
    lookup-unable : (en : "Could not look up exam info", no : "Kunne ikke hente eksamensinfo", nn : "Kunne ikkje henta eksamensinfo"),
    lookup-unable-error-msg : (en : "Error message", no : "Feilbeskjed", nn : "Feilbeskjed"),
    external-service-unavailable : (en: "No external service available", no : "Ingen ekstern tjeneste tilgjengelig", nn : "Ingen ekstern teneste tilgjengeleg")
  }
}
